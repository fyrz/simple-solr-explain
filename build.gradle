apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

// Definition of dependency versions
ext {
    solrVersion = '7.5.0'
    gsonVersion = '2.3.1'
    slf4jVersion = '1.7.24'
    log4jtoslf4jVersion = '2.1'
    commonsloggingVersion = '1.1.1'
    junitVersion = '4.11'
    assertjVersion = '1.7.1'
}

version = '0.1'

idea {
    project {
        languageLevel = '1.7'
    }
}

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    }
}

repositories {
    // Restlet repository: necessary for Solr transitive dependencies
    maven { url "http://maven.restlet.org" }
    mavenCentral()
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
}

dependencies {
    compile("com.google.code.gson:gson:$gsonVersion")
    provided("org.slf4j:slf4j-api:$slf4jVersion")
    provided("org.slf4j:slf4j-simple:$slf4jVersion") 
    //provided("org.apache.logging.log4j:log4j-to-slf4j:$log4jtoslf4jVersion")
    provided("org.apache.solr:solr-test-framework:$solrVersion") {
        exclude(group: 'log4j', module: 'log4j')
    }
    provided("org.apache.solr:solr-core:$solrVersion") {
        exclude(group: "log4j", module: "log4j")
    }
    provided("org.apache.solr:solr-solrj:$solrVersion") {
        exclude(group: "log4j", module: "log4j")
    }

    testCompile("commons-logging:commons-logging:$commonsloggingVersion")
    testCompile("junit:junit:$junitVersion")
    testCompile("org.assertj:assertj-core:$assertjVersion")
}


// Fix test classpath issues in IDEA
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

// Increase Java warn level
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

// Add jacoco coverage
jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

test {
    systemProperty 'java.security.egd', 'file:/dev/./urandom'
}
